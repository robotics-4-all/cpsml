import msg

Model:
    (metadata=Metadata)?
    api=API
;

Metadata:
    'Metadata'
    (
        ('name:' name=ID)
        ('author:' author=STRING)?
        ('description:' description=STRING)?
    )#
    'end'
;

API: AsyncAPI | RestAPI;

AsyncAPI:
    'API<Async>' name=ID
    (
        'broker'
            broker=BrokerInfo
        'end'

        'endpoints'
            endpoints*=Endpoint
        'end'

        'messages'
            msgs*=Message
        'end'
    )#
    'end'
;

RestAPI:
    'NOT_IMPLEMENTED_REST_API'
;

BrokerInfo:
    (
        ('type' ':' btype=BrokerType)
        ('host' ':' host=STRING)
        ('port' ':' port=INT)
        ('ssl' ':' ssl=BOOL)?
        ('username' ':' username=STRING)?
        ('password' ':' password=STRING)?
    )#
;

BrokerType:
    'AMQP'  |
    'MQTT'  |
    'Redis'
;

Endpoint: Publisher | Subscriber | RPC | Action;

Publisher:
    'publisher'
    (
        ('uri:' uri=STRING)
        ('msg:' msg=[TopicMessage:FQN|^msgs])?
        ('namespace:' namespace=STRING)?
    )#
    'end'
;

Subscriber:
    'subscriber'
    (
        ('uri:' uri=STRING)
        ('msg:' msg=[TopicMessage:FQN|^msgs])?
        ('namespace:' namespace=STRING)?
    )#
    'end'
;

RPC:
    'rpc'
    (
        ('uri:' uri=STRING)
        ('msg:' msg=[ReqRespMessage:FQN|^msgs])?
        ('namespace:' namespace=STRING)?
    )#
    'end'
;

Action:
    'action'
    (
        ('uri:' uri=STRING)
        ('msg:' msg=[ActionMessage:FQN|^msgs])?
        ('namespace:' namespace=STRING)?
    )#
    'end'
;

EndpointType:
    'Publisher'     |
    'Subscriber'    |
    'RPC'           |
    'Action'
;

Array:
    "[" values*=Value[','] "]"
;

Value:
    STRING | FLOAT | BOOL | Dict | Array | "null"
;

Dict:
    "{" members*=Member[','] "}"
;

Member:
    key=STRING ':' value=Value
;

NID: /(.*?)\n/;

FQN: ID+['.'];
FQNI: ID+['.']('.*')?;
Import: 'import' importURI=FQNI ('as' name=ID)? ';';

Description: /\#(.*?)$/;

Comment: CommentLine | CommentBlock ;

CommentLine: /\/\/.*?$/;

CommentBlock: /\/\*(.|\n)*?\*\//;

