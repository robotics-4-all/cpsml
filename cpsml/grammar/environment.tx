import thing
import geometry

Model:
    imports*=Import
    (
    (environment=Environment)?
    obstacles*=Obstacle
    )#
;


Environment:
    'Environment' '<' dimensions=EnvDimensions '>' name=ID
    (
        ('properties'
            (properties*=EnvironmentalProperty)
        'end'
        )
        ('things'
            (things=[Thing:FQN] '@' poses=Pose)*
        'end'
        )?
        ('obstacles'
            (obstacles=[Obstacle:FQN] '@' poses=Pose)*
        'end'
        )?
        ('entities'
            (entities=[PerceivableEntity:FQN] '@' poses=Pose)*
        'end'
        )?
    )#
    'end'
;


EnvDimensions: '2D' | '3D';


Obstacle:
    'Obstacle' name=ID
        'shape' ':' shape=Shape
    'end'
;


PerceivableEntity:
    'PerceivableEntity'
        'properties' ':' '-' properties*=PerceptionProperty['-']
        'shape' ':' shape=Shape
    'end'
;


PerceptionProperty:
    type=EnvironmentalPropertyType ':' value=FLOAT ('with range' range=FLOAT)?
        ('with dispersion' dispersion=DispersionType)?
;


PerceptionPropertyType:
    'Temperature' |
    'Humidity'    |
    'Sound'       |
    'Speech'
;


EnvironmentalProperty:
    type=EnvironmentalPropertyType ':' value=FLOAT
        ('with dispersion' dispersion=DispersionType)?
;


EnvironmentalPropertyType:
    'Temperature'  |
    'AirQuality'   |
    'SoundLevelDB' |
    'CO2'          |
    'Gas'          |
    'Illumination' |
    'Humidity'
;


DispersionType:
    'Constant'      |
    'Linear'        |
    'Exponential'   |
    'Logarithmic'   |
    'Quadratic'
;

NID: /(.*?)\n/;

FQN: ID('.'ID)*;
FQNI: ID+['.']('.*')?;
Import: 'import' importURI=FQNI ('as' name=ID)?;

Description: /\#(.*?)$/;

// Special rule for comments. Comments start with //

// Comments
Comment: CommentLine | CommentBlock ;

CommentLine: /\/\/.*?$/;

CommentBlock: /\/\*(.|\n)*?\*\//;
