/*
    Thing DSL
*/

reference comm_idl as idl
import networking
import communication

Model:
    (
    imports*=Import
    things*=Thing
    )#
;


D2NConnection:
    'D2N' from=[Thing] '->' to=[Network]
;


Thing: Robot | Device | Sensor | Actuator;


Robot:
    'Robot' name=ID
        (
            ('type' ':' rtype=RobotType)
            ('boards' ':' '-' boards+=DeviceBoardType['-'])
            ('sensors' ':' '-' sensors*=[Sensor:FQN|+m:things]['-'])?
            ('actuators' ':' '-' actuators*=[Actuator:FQN|+m:things]['-'])?
            ('functionalities' ':' '-' functionalities+=RobotFunctionalities['-'])?
            ('networking' ':' networking=[Network:FQN|+m:networks])?
            ('communication' ':' communication=[CommunicationMiddleware:FQN])?
            ('hasBle' ':' ble=BOOL)?
        )#
    'end'
;

Device:
    'Device' name=ID
        (
            ('type: ' type=DeviceBoardType)
            ('os' ':' os=OperatingSystem)?
            ('sensors:' '-' sensors*=[Sensor:FQN|+m:things]['-'])?
            ('actuators:' '-' actuators*=[Actuator:FQN|+m:things]['-'])?
            ('networking' ':' networking=[Network:FQN|+m:networks])?
            ('communication' ':' communication=[CommunicationMiddleware:FQN])?
            ('hasBle: ' ble=BOOL)?
        )#
    'end'
;

Actuator:
    GenericActuator     |
    MotorController     |
    Speaker             |
    Led
;

GenericSensor:
    'Sensor' name=ID
        (
            ('type:' type=SensorType)
            ('dataModel:' dataModel=[idl.DataType:FQN])
            ('connectivity:' connectivity=DeviceConnectivity)?
            ('freq:' acqFreq=FLOAT)?
            ('maxFreq:' maxFreq=FLOAT)?
        )#
    'end'
;

MotorController:
    'MotorController' name=ID
        (
            ('type:' type=MotorControllerType)
            ('dataModel:' dataModel=[idl.DataType:FQN])
            ('connectivity:' connectivity=DeviceConnectivity)?
        )#
    'end'
;

Speaker:
    'Speaker' name=ID
        (
            ('dataModel:' dataModel=[idl.DataType:FQN])
            ('connectivity:' connectivity=DeviceConnectivity)?
        )#
    'end'
;

Led: SingleLed | LedArray;

SingleLed:
    'Led' name=ID
        (
            ('dataModel:' dataModel=[idl.DataType:FQN])
            ('color' ':' color=INT)?
            ('connectivity:' connectivity=DeviceConnectivity)?
            ('responseTime:' responseTime=FLOAT)?
        )#
    'end'
;

LedArray:
    'LedArray' name=ID
        (
            ('size:' size=INT)
            ('dataModel:' dataModel=[idl.DataType:FQN])
            ('connectivity:' connectivity=DeviceConnectivity)?
            ('responseTime:' responseTime=FLOAT)?
        )#
    'end'
;

GenericActuator:
    'Actuator' name=ID
        (
            ('type:' type=ActuatorType)
            ('dataModel:' dataModel=[idl.DataType:FQN])
            ('connectivity:' connectivity=DeviceConnectivity)?
            ('responseTime:' responseTime=FLOAT)?
        )#
    'end'
;


Sensor:
    GenericSensor   |
    RangeFinder     |
    LiDARSensor     |
    CameraRGB       |
    CameraThermal   |
    Button
;

Button:
    'Button' name=ID
        (
            ('dataModel:' dataModel=[idl.DataType:FQN])
            ('connectivity:' connectivity=DeviceConnectivity)?
            ('maxFreq:' maxFreq=FLOAT)?
        )#
    'end'
;

RangeFinder:
    'RangeFinder' name=ID
        (
            ('type:' type=[RangeFinderType])
            ('dataModel:' dataModel=[idl.DataType:FQN])
            ('connectivity:' connectivity=DeviceConnectivity)?
            ('freq:' pubFreq=FLOAT)?
            ('maxFreq:' maxFreq=FLOAT)?
            ('hfov:' hfov=FLOAT)?
            ('vfov:' vfov=FLOAT)?
            ('minRange:' minRange=FLOAT)?
            ('maxRange:' maxRange=FLOAT)?
        )#
    'end'
;

LiDARSensor:
    'LiDARSensor' name=ID
        (
            ('dataModel:' dataModel=[idl.DataType:FQN])
            ('connectivity:' connectivity=DeviceConnectivity)?
            ('freq:' pubFreq=FLOAT)?
            ('maxFreq:' maxFreq=FLOAT)?
            ('minAngle:' minAngle=FLOAT)?
            ('maxAngle:' maxAngle=FLOAT)?
            ('numRanges:' numRanges=FLOAT)?
            ('minRange:' minRange=FLOAT)?
            ('maxRange:' maxRange=FLOAT)?
        )#
    'end'
;

CameraRGB:
    'CameraRGB' name=ID
        (
            ('dataModel:' dataModel=[idl.DataType:FQN])
            ('connectivity:' connectivity=DeviceConnectivity)?
            ('freq:' pubFreq=FLOAT)?
            ('maxFreq:' maxFreq=FLOAT)?
            ('resHeight:' resHeight=FLOAT)?
            ('resWidth:' resWidth=FLOAT)?
            ('hfov:' hfov=FLOAT)?
            ('vfov:' vfov=FLOAT)?
        )#
    'end'
;

CameraThermal:
    'CameraThermal' name=ID
        (
            ('dataModel:' dataModel=[idl.DataType:FQN])
            ('connectivity:' connectivity=DeviceConnectivity)?
            ('freq:' pubFreq=FLOAT)?
            ('maxFreq:' maxFreq=FLOAT)?
            ('resHeight:' resHeight=FLOAT)?
            ('resWidth:' resWidth=FLOAT)?
            ('hfov:' hfov=FLOAT)?
            ('vfov:' vfov=FLOAT)?
        )#
    'end'
;

Accelerometer:
    'Accelerometer' name=ID
        (
            ('dataModel:' dataModel=[idl.DataType:FQN])
            ('connectivity:' connectivity=DeviceConnectivity)?
            ('freq:' pubFreq=FLOAT)?
            ('maxFreq:' maxFreq=FLOAT)?
        )#
    'end'
;

Magnetometer:
    'Magnetometer' name=ID
        (
            ('dataModel:' dataModel=[idl.DataType:FQN])
            ('connectivity:' connectivity=DeviceConnectivity)?
            ('freq:' pubFreq=FLOAT)?
            ('maxFreq:' maxFreq=FLOAT)?
        )#
    'end'
;

Gyroscope:
    'Gyroscope' name=ID
        (
            ('dataModel:' dataModel=[idl.DataType:FQN])
            ('connectivity:' connectivity=DeviceConnectivity)?
            ('freq:' pubFreq=FLOAT)?
            ('maxFreq:' maxFreq=FLOAT)?
        )#
    'end'
;

IMU:
    'IMU' name=ID
        'type:' type=IMUType
        (
            ('dataModel:' dataModel=[idl.DataType:FQN])
            ('connectivity:' connectivity=[DeviceConnectivity])?
            ('freq:' pubFreq=FLOAT)?
            ('maxFreq:' maxFreq=FLOAT)?
        )#
    'end'
;

IMUType: '6DOF' | '9DOF';

RobotType: 'Humanoid' | 'UGV' | 'UAV';

RobotFunctionalities:
    'Mobility'          |
    'VoiceInterraction' |
    'NLU'               |
    'Navigation'        |
    'Mapping'           |
    'SLAM'              |
    'FaceDetection'     |
    'ObjectDetection'
;

SensorType:
    'Range'         |
    'LiDAR'         |
    'CameraRGB'     |
    'Thermal'       |
    'ThermalArray'  |
    'CameraThermal' |
    'IMU'           |
    'Compass'       |
    'Accelerometer' |
    'Magnetometer'
;

ActuatorType:
    'MotorController'   |
    'LED'               |
    'RGBLed'            |
    'NeopixelLED'       |
    'ServoController'
;

DeviceConnectivity: WirelessConnection | HardwareConnection;

WirelessConnection: 'Wifi' | 'BLE';
HardwareConnection: 'Hardware';

// HardwareConnection: 'SPI' | 'I2C' | 'UART' | 'GPIO' | 'USB' | 'ETH' | 'PWM';

RangeFinderType: 'Sonar' | 'ToF' | 'IR';

DeviceBoardType: 'ESP32' | 'ESP8266' | 'RPi3' | 'RPi4' | 'JetsonNano';

OperatingSystem: 'RaspbianOS' | 'RiotOS' | 'UbuntuOS' | 'OtherOS';

MotorControllerType: 'SingleMotor' | 'DualMotor';

NID: /(.*?)\n/;

FQN: ID('.'ID)*;
FQNI: ID+['.']('.*')?;
Import: 'import' importURI=FQNI ('as' name=ID)?;

Description: /\#(.*?)$/;

// Special rule for comments. Comments start with //

// Comments
Comment: CommentLine | CommentBlock ;

CommentLine: /\/\/.*?$/;

CommentBlock: /\/\*(.|\n)*?\*\//;
